-- SQL Server Instance:  smg-sql01
USE [Utilities];
IF EXISTS (SELECT * FROM sys.objects WHERE type = 'P' AND OBJECT_ID = OBJECT_ID('sl.di_PopulateDailyReportMissingPatientIDsInLabs'))
   DROP PROC [sl].[di_PopulateDailyReportMissingPatientIDsInLabs]
GO
CREATE PROCEDURE [sl].[di_PopulateDailyReportMissingPatientIDsInLabs]
/* -----------------------------------------------------------------------------------------------------------
   Procedure Name  :  sl.di_PopulateDailyReportMissingPatientIDsInLabs
   Business Analyis:
   Project/Process :   
   Description     :  Populate the DailyReportMissingPatientIDsInLabs table.
	  
   Author          :  Philip Morrison 
   Create Date     :  05/06/2025

   ***********************************************************************************************************
   **         Change History                                                                                **
   ***********************************************************************************************************

   Date       Version    Author             Description
   --------   --------   -----------        ------------
   05/06/2025 1.01.001   Philip Morrison    Created
   6/9/2025   1.01.002   Philip Morrison    Standards Corrections

*/ -----------------------------------------------------------------------------------------------------------                                   

AS
BEGIN

-- This Instance Declarations
DECLARE @CurrentYear [int] = 0

SET @CurrentYear = DATEPART(year, getdate());

-- Template Declarations
DECLARE @Application            varchar(128) = 'Summit Life' 
DECLARE @Version                varchar(25)  = '1.00.002'

DECLARE @ProcessID              int          = 212
DECLARE @Process                varchar(128) = 'DailyReports'

DECLARE @BatchOutID             int
DECLARE @BatchDescription       varchar(1000) = @@ServerName + '  - ' + @Version
DECLARE @BatchDetailDescription varchar(1000)
DECLARE @BatchMessage           varchar(MAX)
DECLARE @User                   varchar(128) = SUSER_NAME()

DECLARE @AnticipatedRecordCount int 
DECLARE @ActualRecordCount      int


SET NOCOUNT ON

BEGIN TRY

--  Initialize Batch
    EXEC Admin.Utilities.logs.di_Batch @BatchOutID OUTPUT,  NULL, 'BatchStart', @BatchDescription, @ProcessID, @Process
 ----------------------------------------------------------------------------------------------------------------------------------------------------

    SET @BatchDetailDescription = '010/020:  Truncate DailyReportMissingPatientIDsInLabs'
    EXEC Admin.Utilities.logs.di_Batch @BatchOutID OUTPUT,  @BatchOutID, 'DetailStart', @BatchDetailDescription
	
	  SELECT @AnticipatedRecordCount = COUNT(*)
	                                   FROM [DS_SummitLife].[dbo].[DailyReportMissingPatientIDsInLabs];
	  
    -- Truncate DailyReportMissingPatientIDsInLabs
    TRUNCATE TABLE [DS_SummitLife].[dbo].[DailyReportMissingPatientIDsInLabs];
	
    SET @ActualRecordCount = @@ROWCOUNT
    EXEC Admin.Utilities.logs.di_Batch @BatchOutID OUTPUT,  @BatchOutID, 'DetailEnd', NULL, NULL, NULL, @AnticipatedRecordCount, @ActualRecordCount
----------------------------------------------------------------------------------------------------------------------------------------------------


    SET @BatchDetailDescription = '020/020:  Insert into DailyReportMissingPatientIDsInLabs'
    EXEC Admin.Utilities.logs.di_Batch @BatchOutID OUTPUT,  @BatchOutID, 'DetailStart', @BatchDetailDescription
	
    SELECT @AnticipatedRecordCount = COUNT(*)
                                     FROM [DS_SummitLife].[dbo].[CombineLabAndBiometrics] b
	                                 JOIN [DS_SummitLife].[dbo].[SL_AllEmployees] e
	                                 ON e.[EmployeeNumber] = b.[EmployeeNumber]
                                     WHERE ((b.[PatientID] IS NULL OR len(ltrim(rtrim(b.PatientID))) = 0)
	                                 AND DATEPART(year, CONVERT([datetime],b.[LabDate], 101)) = @CurrentYear
                                     AND LEN(b.[Chol]) > 0
  	                                 AND LEN(b.[HDL]) > 0
                                     AND LEN(b.[TCOverHDL]) > 0
                                     AND LEN(b.[LDL]) > 0
	                                 AND LEN(b.[Triglycerides]) > 0
                                     AND LEN(b.[FastingBloodGlucoseMGOverDL]) > 0
                                     AND LEN(b.[GGTIUOverL]) > 0
                                     AND LEN(b.[HemoglobinA1C]) > 0);
	  
    -- Insert into DailyReportMissingPatientIDsInLabs
    INSERT INTO [DS_SummitLife].[dbo].[DailyReportMissingPatientIDsInLabs]
                                                                           (
                                                                             [LastHireDate]
                                                                             ,[SSN]
                                                                             ,[EmployeeNumber]
                                                                             ,[EmployeeEmail]
                                                                             ,[LastName]
                                                                             ,[FirstName]
                                                                             ,[BirthDate]
                                                                             ,[Gender]
                                                                             ,[EmploymentStatus]
                                                                             ,[FullPartTime]
                                                                             ,[Location]
                                                                             ,[WellnessEnrolled]
                                                                             ,[LabDate]
                                                                             ,[Chol]
                                                                             ,[HDL]
                                                                             ,[TCOverHDL]
                                                                             ,[LDL]
                                                                             ,[LDLCholesterolCOrD]
                                                                             ,[Triglycerides]
                                                                             ,[FastingBloodGlucoseMGOverDL]
                                                                             ,[GGTIUOverL]
                                                                             ,[HemoglobinA1C]
                                                                             ,[Status]
                                                                             ,[PhysicianFormFilledOut]
                                                                             ,[DataGatheredOnTimestamp]
                                                                            ) 				
	SELECT
	    CONVERT([varchar] (10), e.[LastHireDate], 101) AS [LastHireDate]
		,e.[Ssn] AS [SSN]
		,e.[EmployeeNumber] AS [EmployeeNumber]
		,e.[EmployeeEmail] AS [EmployeeEmail]
	    ,e.[LastName] AS [LastName]
	    ,e.[FirstName] AS [FirstName]
	    ,CONVERT([varchar] (10), e.[BirthDate], 101) AS [BirthDate]
	    ,e.[Gender] AS [Gender]
		,e.[EmploymentStatus] AS [EmploymentStatus]
		,e.[Full/PartTime] AS [FullPartTime]
		,e.[Location] AS [Location]
		,e.[WellnessEnrolled] AS [WellnessEnrolled]
		,CONVERT([varchar] (10), b.[LabDate], 101)  AS [LabDate]
		,b.[Chol]  AS [Chol]
		,b.[HDL]  AS [HDL]
		,b.[TCOverHDL]  AS [TCOverHDL]
		,b.[LDL]  AS [LDL]
		,b.[LDLCholesterolCOrD]  AS [LDLCholesterolCOrD]
		,b.[Triglycerides]  AS [Triglycerides]
		,b.[FastingBloodGlucoseMGOverDL]  AS [FastingBloodGlucoseMGOverDL]
		,b.[GGTIUOverL] AS [GGTIUOverL]
		,b.[HemoglobinA1C]  AS [HemoglobinA1C]
		,'Missing Patient IDs' AS [Status]
        ,'' AS [PhysicianFormFilledOut]
		,CONVERT([nvarchar] (50), GETDATE(),121) AS [DataGatheredOnTimestamp]
      FROM [DS_SummitLife].[dbo].[CombineLabAndBiometrics] b
	       JOIN [DS_SummitLife].[dbo].[SL_AllEmployees] e
	         ON e.[EmployeeNumber] = b.[EmployeeNumber]
     WHERE ((b.[PatientID] IS NULL OR len(ltrim(rtrim(b.PatientID))) = 0)
	   AND DATEPART(year, CONVERT([datetime],b.[LabDate], 101)) = @CurrentYear
       AND LEN(b.[Chol]) > 0
  	   AND LEN(b.[HDL]) > 0
       AND LEN(b.[TCOverHDL]) > 0
       AND LEN(b.[LDL]) > 0
	   AND LEN(b.[Triglycerides]) > 0
       AND LEN(b.[FastingBloodGlucoseMGOverDL]) > 0
       AND LEN(b.[GGTIUOverL]) > 0
       AND LEN(b.[HemoglobinA1C]) > 0);
	
    SET @ActualRecordCount = @@ROWCOUNT
    EXEC Admin.Utilities.logs.di_Batch @BatchOutID OUTPUT,  @BatchOutID, 'DetailEnd', NULL, NULL, NULL, @AnticipatedRecordCount, @ActualRecordCount
----------------------------------------------------------------------------------------------------------------------------------------------------

--  Close batch
    EXEC Admin.Utilities.logs.di_batch @BatchOutID OUTPUT, @BatchOutID, 'BatchEnd'
    
END TRY

BEGIN CATCH
DECLARE @Err              int
     ,  @ErrorMessage     varchar(Max)
     ,  @ErrorLine        varchar(128)
     ,  @Workstation      varchar(128) = @Application
     ,  @Procedure        VARCHAR(500)

    IF ERROR_NUMBER() IS NULL 
      SET @Err =0;
    ELSE
      SET @Err = ERROR_NUMBER();

    SET @ErrorMessage = ERROR_MESSAGE()
	
    SET @ErrorLine    = 'SP Line Number: ' + CAST(ERROR_LINE() as varchar(10)) 
    
	SET @Workstation  = HOST_NAME()
	
    SET @Procedure    = @@SERVERNAME + '.' + DB_NAME() + '.' + OBJECT_SCHEMA_NAME(@@ProcID) + '.' + OBJECT_NAME(@@ProcID) + ' - ' + @ErrorLine + ' - ' + LEFT(@BatchDetailDescription, 7)
    EXEC Admin.Utilities.administration.di_ErrorLog  @Application ,@Process, @Version ,0, @ErrorMessage, @Procedure,  @User , @Workstation

    SET @BatchMessage = 'Process Failed:  ' +  @ErrorMessage
    EXEC Admin.Utilities.logs.di_batch @BatchOutID OUTPUT, @BatchOutID, 'BatchEnd', @BatchMessage
    RAISERROR(@ErrorMessage, 16,1)

END CATCH


END